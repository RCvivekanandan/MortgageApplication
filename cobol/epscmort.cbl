000100 ID DIVISION.
000200 PROGRAM-ID. EPSCMORT.
000300*    THIS DEMONSTRATES CICS/DEBUG           - EPSDEMOS 2008
000400*
000500*    THIS PROGRAM WILL RECEIVE A DATE AND COVERT THE DATE TO
000600*    AN INTEGER IN A CALLED PROGRAM TO DETERMINE DAYS FROM
000700*    CURRENT DATE.
000800*
000900*    (C) 2017 IBM - JIM HILDNER RESERVED.
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 SOURCE-COMPUTER. IBM-FLEX-ES.
001300 OBJECT-COMPUTER. IBM-FLEX-ES.
001400*
001500 DATA DIVISION.
001600 WORKING-STORAGE SECTION.
001700*
001800 01  W-FLAGS.
001900     10  W-SEND-FLAG                    PIC X.
002000         88  SEND-ERASE                   VALUE '1'.
002100         88  SEND-DATAONLY                VALUE '2'.
002200         88  SEND-MAPONLY                 VALUE '3'.
002300         88  SEND-DATAONLY-ALARM          VALUE '4'.
002400         88  SEND-ALL                     VALUE '5'.
002500
002600 01 W-CONVERSIONS.
002700     05  W-PMT-CNVRT     PIC X(12).
002800     05  W-PMT-NUMBER
002900         REDEFINES W-PMT-CNVRT
003000                         PIC 9(10)V99.
003100     05  WS-FORMAT-NUMBER PIC Z,ZZZ,ZZ9.99.
003200     05  W-PRINC-CNVRT   PIC X(12).
003300     05  W-PRINC-NUMBER
003400         REDEFINES W-PRINC-CNVRT
003500                         PIC 9(10)V99.
003600
003700 01 W-CALL-PROGRAM                      PIC X(8).
003800*
003900 01 W-RETIREMENT-WA                     PIC 9(4).
004000 01 W-COMAREA-LENGTH                    PIC 9(4) COMP.
004100
004200 01  SQL-ERROR-MSG.
004300     03  FILLER              PIC X(11)      VALUE 'SQL ERROR: '.
004400     03  SQL-ERROR-CODE      PIC 9(5) DISPLAY.
004500*
004600     EXEC SQL
004700         INCLUDE SQLCA
004800     END-EXEC.
004900*
005000     EXEC SQL DECLARE SYSIBM.SYSDUMMY1 TABLE
005100     ( IBMREQD                        CHAR(1) NOT NULL
005200     ) END-EXEC.
005300*
005400 01 IBMREQD                           PIC X(1).
005500*
005600 01  END-OF-TRANS-MSG                 PIC X(30)
005700       VALUE 'END OF TRANSACTION - THANK YOU'.
005800 01  BLANK-MSG                        PIC X(1) VALUE ' '.
005900     COPY DFHAID.
006000*    COPY DFHEIBLK.
006100     COPY EPSMORT.
006200
006300 01  W-COMMUNICATION-AREA.
006400     COPY EPSMTCOM.
006500
006600 COPY EPSNBRPM.
006700
006800 LINKAGE SECTION.
006900
007000 01 DFHCOMMAREA.
007100 COPY EPSMTCOM.
007200
007300 PROCEDURE DIVISION USING DFHCOMMAREA.
007400
007500 EPSCMORT-MAINLINE.
007600* Call procedure to do SQL call
007700     PERFORM A805-DUMMY-SQL-CALL
007800     MOVE LENGTH OF DFHCOMMAREA to W-COMAREA-LENGTH.
007900     MOVE DFHCOMMAREA to W-COMMUNICATION-AREA.
008000     EVALUATE TRUE
008100         WHEN EIBCALEN = ZERO
008200* First time in - Show Screen
008300             MOVE LOW-VALUES TO EPMENUO
008400             SET SEND-ERASE TO TRUE
008500             PERFORM A300-SEND-MAP
008600             MOVE '3' TO
008700                PROCESS-INDICATOR OF W-COMMUNICATION-AREA
008800         WHEN EIBAID = DFHCLEAR
008900* Process CLEAR key
009000             MOVE LOW-VALUES TO EPMENUO
009100             SET SEND-ERASE TO TRUE
009200             PERFORM A300-SEND-MAP
009300         WHEN EIBAID = DFHPF3 OR DFHPF12
009400* Process END/RETURN keys
009500            IF PROCESS-INDICATOR OF W-COMMUNICATION-AREA = '3'
009600                EXEC CICS
009700                   SEND TEXT FROM (END-OF-TRANS-MSG)
009800                   ERASE
009900                   FREEKB
010000                END-EXEC
010100                EXEC CICS
010200                     RETURN
010300                END-EXEC
010400             ELSE
010500                SET SEND-ALL TO TRUE
010600                EXEC CICS
010700                   SEND TEXT FROM (BLANK-MSG)
010800                   ERASE
010900                   FREEKB
011000                END-EXEC
011100                PERFORM A300-SEND-MAP
011200                MOVE '3' TO
011300                    PROCESS-INDICATOR OF W-COMMUNICATION-AREA
011400             END-IF
011401
011402
011500         WHEN EIBAID = DFHPF9
011600             MOVE '9' TO
011700                PROCESS-INDICATOR OF W-COMMUNICATION-AREA
011800             EXEC CICS LINK PROGRAM( 'EPSMLIST' )
011900                    COMMAREA( W-COMMUNICATION-AREA )
012000             END-EXEC
012100         WHEN EIBAID = DFHENTER
012200* Process ENTER Key
012300             IF PROCESS-INDICATOR OF W-COMMUNICATION-AREA = '3'
012400                PERFORM A100-PROCESS-MAP
012500             ELSE
012600                EXEC CICS LINK PROGRAM('EPSMLIST')
012700                       COMMAREA( W-COMMUNICATION-AREA )
012800                END-EXEC
012900             END-IF
013000         WHEN OTHER
013100* Process Data
013200              IF PROCESS-INDICATOR OF W-COMMUNICATION-AREA = '3'
013300                PERFORM A600-CALCULATE-MORTGAGE
013400                EXEC CICS RETURN END-EXEC
013500*             ELSE
013600*                MOVE X'E8' TO MSGERRA
013700*                MOVE LOW-VALUES TO EPMENUO
013800*                SET SEND-DATAONLY-ALARM TO TRUE
013900*                PERFORM A300-SEND-MAP
014000              END-IF
014100     END-EVALUATE
014200     EXEC CICS
014300         RETURN TRANSID('EPSP')
014400         COMMAREA(W-COMMUNICATION-AREA)
014500         LENGTH(W-COMAREA-LENGTH)
014600     END-EXEC.
014700
014800 A100-PROCESS-MAP.
014900     PERFORM A400-RECEIVE-MAP.
015000     PERFORM A600-CALCULATE-MORTGAGE
015100     SET SEND-DATAONLY TO TRUE
015200     PERFORM A300-SEND-MAP
015300         .
015400
015500 A300-SEND-MAP.
015600     EVALUATE TRUE
015700        WHEN SEND-MAPONLY
015800             EXEC CICS
015900               SEND MAP ('EPMENU')
016000                 MAPSET('EPSMORT')
016100                 MAPONLY
016200                 CURSOR
016300             END-EXEC
016400        WHEN SEND-ERASE
016500             EXEC CICS
016600               SEND MAP ('EPMENU')
016700                   MAPSET('EPSMORT')
016800                   FROM(EPMENUO)
016900                   ERASE
017000                   CURSOR
017100             END-EXEC
017200        WHEN SEND-DATAONLY
017300             EXEC CICS
017400               SEND MAP ('EPMENU')
017500                   MAPSET('EPSMORT')
017600                   FROM(EPMENUO)
017700                   DATAONLY
017800                   CURSOR
017900             END-EXEC
018000        WHEN SEND-ALL
018100             EXEC CICS
018200               SEND MAP ('EPMENU')
018300                   MAPSET('EPSMORT')
018400                   FROM(EPMENUO)
018500               END-EXEC.
018600
018700 A400-RECEIVE-MAP.
018800     EXEC CICS
018900          RECEIVE MAP('EPMENU')
019000             MAPSET('EPSMORT')
019100             INTO (EPMENUI)
019200     END-EXEC.
019300
019400     MOVE EPLOANI        TO EPSPARM-VALIDATE-DATA.
019500     MOVE LENGTH OF EPLOANI
019600                         TO EPSPARM-MAX-LENGTH.
019700     CALL 'EPSNBRVL' USING EPS-NUMBER-VALIDATION.
019800     COMPUTE EPSPCOM-PRINCIPLE-DATA
019900          OF W-COMMUNICATION-AREA
020000          = EPSPARM-NUMBER + EPSPARM-DECIMAL.
020100
020200     MOVE EPYEARSI             TO EPSPARM-VALIDATE-DATA.
020300     MOVE LENGTH OF EPYEARSI   TO EPSPARM-MAX-LENGTH.
020400     CALL 'EPSNBRVL' USING EPS-NUMBER-VALIDATION.
020500     COMPUTE EPSPCOM-NUMBER-OF-YEARS
020600          OF W-COMMUNICATION-AREA
020700          = EPSPARM-NUMBER + EPSPARM-DECIMAL.
020800
020900     MOVE EPRATEI              TO EPSPARM-VALIDATE-DATA.
021000     MOVE LENGTH OF EPRATEI    TO EPSPARM-MAX-LENGTH.
021100     CALL 'EPSNBRVL' USING EPS-NUMBER-VALIDATION.
021200     COMPUTE EPSPCOM-QUOTED-INTEREST-RATE
021300          OF W-COMMUNICATION-AREA
021400          = EPSPARM-NUMBER + EPSPARM-DECIMAL.
021500
021600
021700 A600-CALCULATE-MORTGAGE.
021800     MOVE 'Y' TO EPSPCOM-YEAR-MONTH-IND
021900                     OF W-COMMUNICATION-AREA.
022000     MOVE 'EPSCSMRT' TO W-CALL-PROGRAM
022100     EXEC CICS LINK PROGRAM( W-CALL-PROGRAM )
022200                    COMMAREA( W-COMMUNICATION-AREA )
022300     END-EXEC
022400     .
022500     MOVE EPSPCOM-RETURN-MONTH-PAYMENT
022600                       OF W-COMMUNICATION-AREA
022700                       TO WS-FORMAT-NUMBER.
022800
022900     MOVE WS-FORMAT-NUMBER
023000                       TO EPPAYMNTO.
023100     MOVE EPSPCOM-ERRMSG
023200                       OF W-COMMUNICATION-AREA
023300                       TO MSGERRO.
023400
023500 A805-DUMMY-SQL-CALL.
023600     EXEC SQL
023700         SELECT IBMREQD
023800              INTO :IBMREQD
023900              FROM SYSIBM.SYSDUMMY1
024000     END-EXEC.
024100*
024200     IF SQLCODE = 100
024300         MOVE 'No rows found on SYSDUMM1.' TO MSGERRO
024400     ELSE
024500         IF SQLCODE NOT = 0
024600             MOVE SQLCODE TO SQL-ERROR-CODE
024700             MOVE SQL-ERROR-MSG TO MSGERRO
024800         END-IF
024900     END-IF.
025000*
025100